```
tip: 135 
title: Shielded Token Contract 
author: federico<federico.zhen@tron.network>, leo<leo.hou@tron.network>
discussions-to: https://github.com/tronprotocol/tips/issues/135
status: draft
type: Standards Track
category: TRC
created: 2020-03-04
```

## Simple Summary

This TIP provides the contract implementation of transforming public TRC-20 token to shielded token, which can guarantee the privacy of token ownership and transactions. 

## Abstract

The shielded token contract has three core modules: `mint`, `transfer` and `burn`. `mint` is used to transform the public TRC-20 token to shielded token, which makes token ownership invisible. `transfer` is used for shielded token transactions, which can hide the source address, the destination address, and transaction amount. `burn` is used to transform the shielded token to the public TRC-20 token. The technical implementaion is based on zk-SNARK proof system, which is secure and efficient.  

## Motivation

TRC-20 token contract allows users to issue and transfer tokens, but it can not guarantee the privacy, since it leaks the token ownership. When transferring the token, the source address, destination address, and the token amount are  public. The shielded token contract aims to solve this problem and provides users better privacy of token ownership and transactions.

## Specification

The following defines the  implementation of shielded token contract. 

### Preliminaries
**NoteCommitment**

The TRC-20 token is transformed into shielded token in the form of commitment by cryptographic method, which is defined as:

note<sub>-</sub>commitment = NoteCommitment<sub>r</sub>(v)

The `v` value is hidden by  `note_commitment` with the blinding factor `r`.

**SpendDescription**

The `SpendDescription` includes zk-SNARK proof related data, which is used as the input of shielded token transaction. 

```text  
message SpendDescription { 
  bytes value_commitment = 1; // value commitment
  bytes anchor = 2; // merkle root
  bytes nullifier = 3; // used for check double spend
  bytes rk = 4; // used for check spend spend authority signature
  bytes zkproof = 5; // zk-SNARK proof
  bytes spend_authority_signature = 6; //spend authority signature 
}
```

**ReceiveDescription**

The `ReceiveDescrion` also includes zk-SNARK proof related data, which is used as the output of shielded token transaction.

```text  
message ReceiveDescription { 
  bytes value_commitment = 1; // value commitment used for binding signature
  byte  note_commitment = 2;  // note commitment used for hiding the value
  bytes epk = 3;  // ephemeral public key used for encryption
  bytes c_enc = 4;  // encryption for note plaintext
  bytes c_out = 5; // encryption for audit
  bytes zkproof = 6; // zk-SNARK proof 
}
```

For more details about `note commitment`, `SpendDescription` and `ReceiveDescripton`, please refer the [TRONZ shielded transaction protocol](https://www.tronz.io).

### Methods

**constructor**

The `constructor` method binds the TRC-20 token address to shield token contract when it is deployed.

```
  constructor (uint256 trc20ContractAddress) public {
      _owner = msg.sender;
      _trc20Token = TokenTRC20(address(trc20ContractAddress));
  }
```

**mint**

`mint` method is to transform the public TRC-20 token with amount `value` to shielded token in the form of `note_commitment`. The method input parameters also include zk-SNARK proof related data.

```
    // output: cm, cv, epk, proof
    function mint(uint64 value, bytes32[9] calldata output, bytes32[2] calldata bindingSignature, bytes32[21] calldata c) external {
        // step 1: check the value and the note commitments
        address sender = msg.sender;
        require(value > 0, "Mint negative value.");
        require(notecommitment[output[0]] == 0, "Duplicate notecommitments");
        
        // step 2: check the zk-SNARK proof 
        bytes32 signHash = sha256(abi.encodePacked(address(this), value, output, c));
        (bool result,bytes memory msg) = verifyProofContract.call(abi.encode(output, bindingSignature, value, signHash, frontier, leafCount));
        require(result, "The proof and signature have not been verified by the contract");

        //step 3: store the note_commitment in the Merkle tree
        uint256 slot = uint8(msg[0]);
        uint256 nodeIndex = leafCount + 2 ** 32 - 1;
        tree[nodeIndex] = output[0];
        if(slot == 0){
            frontier[0] = output[0];
        }
        for (uint256 i = 1; i < slot+1; i++) {
            nodeIndex = (nodeIndex - 1) / 2;
            tree[nodeIndex] = bytesToBytes32(msg, i*32-31);
            if(i == slot){
                frontier[slot] = tree[nodeIndex];
            }
        }
        latestRoot = bytesToBytes32(msg, slot*32+1);
        
        //step 4: store the latest root in the contract
        roots[latestRoot] = latestRoot;
        leafCount ++;
        
        //step 5: emit the event
        emit newLeaf(leafCount-1, output[0], output[1], output[2], c);
        
        // step 6: transfer the TRC-20 Tokens from the sender to this contract
        bool transferResult = _trc20Token.transferFrom(sender, address(this), value);
        require(transferResult, "TransferFrom failed.");
        emit tokenMint(sender, value);
    }
```

The `mint` includes the six steps:

(1) check the `value` is positive and prevent duplicate note commitments;

(2) verify the proof to check the validity of `note_commitment`by [verifyMintProof](https://github.com/tronprotocol/tips/blob/master/tip-137.md) function;

(3) store  the `note_commitment` in the merkle tree `tree`, which is a mapping data structure.

```
mapping(uint256 => bytes32) public tree;
```

(4) store the latest root in the `roots`, which is used for merkle path proof.

```
mapping(bytes32 => bytes32) public roots; 
```

(5) emit the related data ( `position`, `note_commitment`, `value_commitment`, `epk`, `c_enc`, `c_out`) as event to make it convenient for shielded note scanning.

 ```
event newLeaf(uint256 position, bytes32 cm, bytes32 cv, bytes32 epk, bytes32[21] c);
 ```

(6) transfer the TRC-20 token to the shielded token contract by `transferFrom` function.

**transfer**

`transfer`method is used for shielded token transfer with one input and two outputs. 

```
    //input: cv, rk, spend_auth_sig, proof
    //output1: cv, cm, epk, proof
    //output2: cv, cm, epk, proof
    function transfer(bytes32[10][] calldata input, bytes32[2][] calldata spend_auth_sig, bytes32[9][] calldata output, bytes32[2] calldata bindingSignature, bytes32[21][] calldata c) external {
        // step 1: check the parameters 
        require(input.length>=1 && input.length <=2, "input number must be 1 or 2");
        require(input.length == spend_auth_sig.length, "input number must be equal to spend_auth_sig number");
        for(i = 0; i < input.length; i++){
            //require(nullifiers[input[i][0]] == 0, "The notecommitment being spent has already been nullified!");
            require(roots[input[i][1]] != 0, "The anchor must exist");
        }
        for(i = 0; i < output.length; i++){
            require(notecommitment[output[i][0]] == 0, "Duplicate notecommitment");
        }
        require(output.length>=1 && output.length <=2, "output number must be 1 or 2");
        require(output.length == c.length, "output number must be equal to c number");
        
        //step 2: check the proof 
        bytes32 signHash = sha256(abi.encodePacked(address(this), input, output, c));
        (bool result,bytes memory msg) = verifyProofContract.call(abi.encode(input, spend_auth_sig, output, bindingSignature, signHash, frontier, leafCount));
        require(result, "The proof and signature has not been verified by the contract");

        //step 3: store the output note_commitments in the merkle tree 
        i = 0;
        uint256 j = 0;//msg offset
        while (i < output.length) {
            uint slot = uint8(msg[j]);
            j = j + 1;
            uint256 nodeIndex = leafCount + 2 ** 32 - 1 + i;
            tree[nodeIndex] = output[i][0];//cm
            if(slot == 0){
                frontier[0] = output[i][0];
            }
            for (uint256 k = 1; k < slot+1; k++) {
                nodeIndex = (nodeIndex - 1) / 2;
                tree[nodeIndex] = bytesToBytes32(msg, j);
                j = j + 32;
                if(k == slot){
                    frontier[slot] = tree[nodeIndex];
                }
            }
            i++;
        }
        
        //step 4: store the latest root and nullifier in the contract
        latestRoot = bytesToBytes32(msg, j);
        roots[latestRoot] = latestRoot;
        leafCount = leafCount + output.length;
        for(i = 0; i < input.length; i++){
            bytes32 nf = input[i][0];
            nullifiers[nf] = nf;
        }
        
        //step 5: emit the event
        for(i = 0; i < output.length; i++){
            emit newLeaf(leafCount-(output.length-i), output[i][0], output[i][1], output[i][2], c[i]);
        }
    }   
```

The `transfer` method includes the four steps:

(1) check the size of `input` and `output` to be in the range `[1,2]` , which corresponds the input and output number in the UTXO model; check `nullifiers` to prevent the double spend. `nullifiers` is defined as a mapping structure in the contract. check the `notecommitment` to prevent the duplicate note commitments.

```
mapping(bytes32 => bytes32) public nullifiers; 
```

(2) verify the proof by [verifyTransferProof](https://github.com/tronprotocol/tips/blob/master/tip-137.md)  function check the validity of the  `note_commitments`;

(3) store  the output `note_commitments` in the merkle tree `tree`.

(4) store the latest root in `roots` and the input nullifer in the `nullifiers` .

(5) emit the related data ( `position`, `note_commitment`, `value_commitment`, `epk`, `c_enc`, `c_out`) as event to make it convenient for shielded note scanning.

**burn**

`burn`method is to transform shielded token `note_commitment` to the public TRC-20 token with amount `value`.

```
    //input: cv, rk, spend_auth_sig, proof
    function burn(bytes32[10] calldata input, bytes32[2] calldata spend_auth_sig, uint64 value, bytes32[2] calldata bindingSignature, uint256 payToAddress) external {
        //step 1: check the validity of parameters
        bytes32 nf = input[0];
        bytes32 anchor = input[1];
        require(value > 0, "Mint negative value.");
        require(nullifiers[nf] == 0, "The notecommitment being spent has already been nullified!");
        require(roots[anchor] != 0, "The anchor must exist");
        address payTo = address(payToAddress);

        //step 2: check the zk-SNARK proof 
        bytes32 signHash = sha256(abi.encodePacked(address(this), input, payTo, value));
        (bool result,bytes memory msg) = verifyProofContract.call(abi.encode(input, spend_auth_sig, value, bindingSignature, signHash));
        require(result, "The proof and signature have not been verified by the contract");
        
        //step 3: store nullifier in the contract
        nullifiers[nf] = nf;
        
        //step 4: transfer TRC-20 token from this contract to the nominated address
        bool transferResult = _trc20Token.transfer(payTo, value);
        require(transferResult, "transfer failed.");
        emit tokenBurn(payTo, value);
    }
```

The `burn` includes the four steps:

(1) check the validity of  `value`, `nullifier` and  `anchor`;

(2) verify the proof to check the validity of `note_commitment`by [verifyBurnProof](https://github.com/tronprotocol/tips/blob/master/tip-137.md)  instruction;

(3) store  the `nullifier` in the contract.

(4) transfer the TRC-20 token from the shielded token contract to `payTo` address by `transfer` function.

**getPath**

In order to make it convenient for users to construct zk-SNARK proof, the shielded token contract provides `getPath` function to return the merkle tree path for the given `note_commitment` with `position` parameter.

```
 function getPath(uint256 position) external {
        ...
    }
```

For more technical details of Merkle tree implementation, please refer [timber](https://github.com/EYBlockchain/timber/blob/master/README.md).

### Privacy Protection

The shielded token contract aims to provide users better privacy of token ownership and transactions. The `transfer` function can completely hide the source address, destination address, and amount for token transaction. But it still has some limitations. For `mint`(`burn`) function, the TRC-20 source (destination) address and amount are public, it may leak some information. Furthermore, triggering the contract involves the public address, which may leak the ownership of `note_commitment`. To solve the problem, triggering the contract can be delegated to a third party (refer [EIP-1077](https://github.com/ethereum/EIPs/blob/master/EIPS/eip-1077.md)) or GSN (Gas Station Network, refer [EIP-1613](https://github.com/ethereum/EIPs/blob/master/EIPS/eip-1613.md)).      

## Rationale

Transaction privacy protection is quite important for blockchain. Providing the shielded token contract will help to attract more users because it can provide better privacy for token transactions and promotes the widespread adoption of TRC-20 token and related DApps.


## Implementation 

* https://github.com/tronprotocol/java-tron/blob/feature/shieldedUSDT/deploy/PrivateUSDT.sol
* https://github.com/tronprotocol/java-tron/blob/feature/shielded_TRC20_contract/actuator/src/main/java/org/tron/core/vm/PrecompiledContracts.java
